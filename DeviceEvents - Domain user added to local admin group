DeviceEvents
// Lookback time
| where TimeGenerated > ago(30m)
| where ActionType == "UserAccountAddedToLocalGroup"
    and AdditionalFields.GroupDomainName == "Builtin"
    and AdditionalFields.GroupName == "Administrators"
| where InitiatingProcessAccountSid != "S-1-5-18"
| join kind=inner (
    IdentityInfo
    | where TimeGenerated > ago(30d)
    | summarize arg_max(TimeGenerated, *) by AccountSID
    | project AccountSID, AccountName, AccountDomain
) on $left.AccountSid == $right.AccountSID
| extend EasternTime = datetime_utc_to_local(TimeGenerated, "US/Eastern")
| extend Actor = InitiatingProcessAccountName
| extend AccountAdded = AccountName1
| project EasternTime,DeviceName,Actor,AccountAdded
